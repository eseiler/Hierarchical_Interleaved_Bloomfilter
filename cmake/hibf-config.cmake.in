# SPDX-FileCopyrightText: 2006-2024, Knut Reinert & Freie Universität Berlin
# SPDX-FileCopyrightText: 2016-2024, Knut Reinert & MPI für molekulare Genetik
# SPDX-License-Identifier: BSD-3-Clause

@PACKAGE_INIT@

include (CheckIPOSupported)
include (CMakeDependentOption)
include (CMakeFindDependencyMacro)

find_dependency (cereal @HIBF_CEREAL_VERSION@ REQUIRED)

find_dependency (OpenMP REQUIRED COMPONENTS CXX)

set (THREADS_PREFER_PTHREAD_FLAG TRUE)
find_dependency (Threads QUIET)

find_package (PkgConfig QUIET)
if (PKG_CONFIG_FOUND)
    pkg_check_modules (simde QUIET simde>=@HIBF_SIMDE_VERSION@)
endif ()

if (simde_FOUND)
    add_library (simde IMPORTED INTERFACE)
    add_library (simde::simde ALIAS simde)
    target_include_directories (simde INTERFACE ${simde_INCLUDEDIR})
else ()
    find_dependency (simde @HIBF_SIMDE_VERSION@ REQUIRED)
endif ()

check_ipo_supported (RESULT HIBF_HAS_LTO OUTPUT HIBF_HAS_LTO_OUTPUT)
cmake_dependent_option (HIBF_DEV_CHECK_LTO "LTO check." ON "HIBF_HAS_LTO;NOT CMAKE_INTERPROCEDURAL_OPTIMIZATION" OFF)

if (HIBF_DEV_CHECK_LTO)
    message (FATAL_ERROR " HIBF heavily benefits from Link Time Optimisation (LTO).\n"
                         " Your compiler supports LTO, but is has not been enabled for your project.\n \n"
                         " Add the following at the beginning of your CMakeLists.txt:\n"
                         " ```\n"
                         " include (CheckIPOSupported)\n"
                         " check_ipo_supported (RESULT result OUTPUT output)\n"
                         " if (result)\n"
                         "     set (CMAKE_INTERPROCEDURAL_OPTIMIZATION TRUE)\n"
                         " endif ()\n"
                         " ```"
                         " \n \n"
                         " For development purposes, this error can be disabled via `HIBF_DEV_CHECK_LTO=OFF`.")
endif ()

include ("${CMAKE_CURRENT_LIST_DIR}/hibf-targets.cmake")
